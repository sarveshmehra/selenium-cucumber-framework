group 'webApp-Integration-Tests'
version '1.0'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    swagger
}

sourceSets {
    main {
        java {
            srcDirs += ['src/generated/java']
        }
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

test {
    systemProperties System.getProperties()
    useTestNG() {
        outputDirectory = file("build/test-output")
        listeners << 'runner.ExtentReportListener'
        suites 'src/test/java/runner/'+systemProperties.get("testfile", "combinesuite.xml")
        setParallel('tests')
        setThreadCount(2)
    }
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

task generateCodeFromSwagger {
    doFirst {

        javaexec {
            main = 'io.swagger.codegen.SwaggerCodegen'
            classpath = configurations.swagger
            args = [
                    'generate',
                    '-i', ext.totpSvcSwagger,
                    '-l', 'java',
                    '-o', ext.tempDir,
                    '--model-package', 'com.company.test.totp.svc.model',
                    '--api-package', 'com.company.test.totp.svc.client',
                    '--library', 'okhttp-gson'
            ]
        }
        javaexec {
            main = 'io.swagger.codegen.SwaggerCodegen'
            classpath = configurations.swagger
            args = [
                    'generate',
                    '-i', ext.totpOrcSwagger,
                    '-l', 'java',
                    '-o', ext.tempDir,
                    '--model-package', 'com.company.test.totp.orc.model',
                    '--api-package', 'com.company.test.totp.orc.client',
                    '--library', 'okhttp-gson'
            ]
        }
    }

    doLast {
        copy {
            from "${ext.tempDir}/src/main/java/com/company/test/totp"
            into ext.totpDestDir
            include '**/*.java'
        }
    }

    ext.totpSvcSwagger = 'src/test/resources/json/app-totp-s-v1.json';
    ext.totpOrcSwagger = 'src/test/resources/json/app-totp-o-v1.json';
    ext.tempDir = new File(buildDir, 'swagger/')
    ext.totpDestDir = new File('src/generated/java/com/company/test/totp')
    outputs.dir ext.tempDir
    inputs.file ext.totpSvcSwagger
    inputs.file ext.totpOrcSwagger
    group('build')
}

clean.doLast {
    new File('src/generated/').deleteDir()
}

compileJava {
    dependsOn.add(generateCodeFromSwagger)
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
    compile group: 'io.cucumber', name: 'cucumber-testng', version: '4.2.0'
    testCompile group: 'io.cucumber', name: 'cucumber-java', version: '4.2.0'
    compile group: 'io.cucumber', name: 'cucumber-core', version: '4.2.0'
    compile group: 'io.cucumber', name: 'cucumber-jvm-deps', version: '1.0.6'
    compile group: 'io.cucumber', name: 'cucumber-jvm', version: '4.2.0'
    testCompile group: 'net.sourceforge.cobertura', name: 'cobertura', version: '2.1.1'
    compile group: 'net.masterthought', name: 'cucumber-reporting', version: '4.2.3'
    compile group: 'io.cucumber', name: 'gherkin', version: '5.1.0'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10.1'

    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.5'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile group: 'info.cukes', name: 'cucumber-picocontainer', version: '1.2.5'

    swagger group: 'io.swagger', name: 'swagger-codegen-cli', version: '2.2.1'
    compile 'io.swagger:swagger-annotations:1.5.8'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okhttp:logging-interceptor:2.7.5'
    compile 'joda-time:joda-time:2.9.3'
    compile 'org.codehaus.groovy:groovy-all:2.4.3'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.6'

    compile group: 'com.company.api', name: 'access-token-retriever', version: '0.0.3'
    compile group: 'com.company.service.support', name: 'spring-rest-api-support', version: '2.2.0'
    compile 'com.company.api.testsupport:lm-test-support:0.1.0-SNAPSHOT'
    compile group:'com.company.login', name: 'simple-scim-client', version: '2.1.0', changing: true
    compile group:'com.company.login', name: 'simple-scim-client-test-utils', version: '2.1.0', changing: true
    compile 'com.company.mail:smtp-test-client-lib:1.0.0'
    compile 'com.company.mail:smtp-test-common:1.0.0'

    compile group: 'com.relevantcodes', name: 'extentreports', version: '2.41.2'
}
